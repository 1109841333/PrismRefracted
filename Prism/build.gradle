plugins {
    id "com.github.johnrengelman.shadow" version "7.0.0"
}

apply plugin: 'java'

repositories {
    mavenLocal()
    mavenCentral()

    // Paper
    maven { url = 'https://papermc.io/repo/repository/maven-public/' }

    // Spigot
    maven { url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' }

    // WorldEdit
    maven { url = "https://maven.enginehub.org/repo/" }
}

group = "me.botsko"
version = project.property("version")
sourceCompatibility = targetCompatibility = JavaVersion.VERSION_16

dependencies {
    compileOnly 'org.spigotmc:spigot-api:1.17-R0.1-SNAPSHOT'
    compileOnly 'com.sk89q.worldedit:worldedit-core:7.2.0-SNAPSHOT'
    compileOnly 'com.sk89q.worldedit:worldedit-bukkit:7.2.0-SNAPSHOT'
    implementation 'io.papermc:paperlib:1.0.3'
    implementation 'com.zaxxer:HikariCP:4.0.3'
    implementation 'net.kyori:adventure-api:4.8.1'
    implementation 'net.kyori:adventure-platform-bukkit:4.0.0-SNAPSHOT'
    implementation 'net.kyori:adventure-text-serializer-plain:4.8.1'
    implementation 'org.bstats:bstats-bukkit:1.7'
    implementation 'org.kitteh:paste-gg-api:1.0.0-SNAPSHOT'
    implementation project(':Prism-Api')
}

jar {
    actions = []
    dependsOn = []
    dependsOn('shadowJar')
}

shadowJar {
    dependencies {
        exclude(dependency('org.checkerframework:.*'))
        exclude(dependency('org.slf4j:.*'))
        exclude(dependency('org.intellij:.*'))
        exclude(dependency('org.jetbrains:.*'))
    }

    relocate 'io.papermc.lib', 'me.botsko.prism.paperlib'
    relocate 'com.zaxxer', 'me.botsko.prism.zaxxer'
    relocate 'net.kyori', 'me.botsko.prism.kyori'
    relocate 'org.kitteh', 'me.botsko.prism.kitteh'
    relocate 'org.bstats', 'me.botsko.prism.bstats'
}
